# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "cmx_metadata.cc",
    "cmx_metadata.h",
    "component_controller_impl.cc",
    "component_controller_impl.h",
    "dynamic_library_loader.cc",
    "dynamic_library_loader.h",
    "environment_controller_impl.cc",
    "environment_controller_impl.h",
    "hub/component_hub.cc",
    "hub/component_hub.h",
    "hub/hub.cc",
    "hub/hub.h",
    "hub/hub_info.cc",
    "hub/hub_info.h",
    "hub/realm_hub.cc",
    "hub/realm_hub.h",
    "namespace.cc",
    "namespace.h",
    "namespace_builder.cc",
    "namespace_builder.h",
    "realm.cc",
    "realm.h",
    "root_loader.cc",
    "root_loader.h",
    "runner_holder.cc",
    "runner_holder.h",
    "runtime_metadata.cc",
    "runtime_metadata.h",
    "sandbox_metadata.cc",
    "sandbox_metadata.h",
    "url_resolver.cc",
    "url_resolver.h",
  ]

  public_deps = [
    "//garnet/lib/farfs",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/app/fidl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/svc/cpp",
    "//third_party/rapidjson",
    "//zircon/public/fidl/process",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/launchpad",
    "//zircon/public/lib/loader-service",
    "//zircon/public/lib/zx",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("bin") {
  output_name = "appmgr"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]
}

executable("tests") {
  testonly = true

  output_name = "appmgr_unittests"

  sources = [
    "cmx_metadata_unittest.cc",
    "hub/hub_holder_unittest.cc",
    "namespace_builder_unittest.cc",
    "runtime_metadata_unittest.cc",
    "sandbox_metadata_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/fxl/test:gtest_main",
  ]
}

package("appmgr") {
  deprecated_system_image = true

  deps = [
    ":bin",
  ]
  binaries = [
    {
      name = "appmgr"
    },
  ]
}

package("appmgr_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":tests",
  ]
  tests = [
    {
      name = "appmgr_unittests"
    },
  ]
}
