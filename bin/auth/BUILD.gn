# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

package("token_manager") {
  deps = [
    "//garnet/bin/auth/token_manager",
  ]

  binary = "token_manager"

  meta = [ {
    path = rebase_path("meta/sandbox")
    dest = "sandbox"
  } ]
}

package("tests") {
  testonly = true
  system_image = true

  package_name = "token_manager_tests"

  deps = [
    ":token_manager_unittests",
    "//garnet/bin/auth/test:e2e_dev",
    "//garnet/bin/auth/token_manager/test:dev_auth_provider",
  ]

  binaries = [
    {
      name = "dev_auth_provider"
    },
  ]

  tests = [
    {
      name = "token_manager_unittests"
    },
    {
      name = "dev_token_manager_e2e_test"

      # Disabled because it fails when fvm.blk is used instead of bootfs.
      disabled = true
    },
  ]
}

executable("token_manager_unittests") {
  testonly = true

  deps = [
    "//garnet/public/lib/fxl:fxl_printers",
    "//garnet/public/lib/test_runner/cpp:gtest_main",
    "//garnet/bin/auth/cache:unittests",
    "//garnet/bin/auth/store:unittests",
    "//garnet/bin/auth/test:unittests",
  ]
}
