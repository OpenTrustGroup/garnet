# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//third_party/protobuf/proto_library.gni")

package("cobalt") {
  deps = [
    ":bin",
    "//third_party/cobalt_config",
  ]

  binary = "cobalt"

  meta = [ {
        path = rebase_path("meta/sandbox")
        dest = "sandbox"
      } ]

  resources = [ {
    path = rebase_path(get_label_info("//third_party/cobalt_config", "target_gen_dir") + "/cobalt_config.binproto")
    dest = "cobalt_config.binproto"
  } ]
}

package("cobalt_system_metrics") {
  # TODO(azani): Remove when started as part of boot.
  system_image = true

  deps = [
    ":cobalt_system_metrics_bin",
  ]

  binaries = [
    {
      name = "cobalt_system_metrics"
    },
  ]
}

package("cobalt_tests") {
  testonly = true
  system_image = true


  deps = [
    ":cobalt_testapp",
    ":cobalt_testapp_no_environment",
    ":cobalt_testapp_no_network",
    ":cobalt_unittests",
  ]

  binaries = [
    {
      name = "cobalt_testapp"
    },
  ]

  tests = [
    {
      name = "cobalt_unittests"
    },

    {
      name = "cobalt_testapp_no_network"
    },

    {
      name = "cobalt_testapp_no_environment"

      # We don't want to run this version cobalt_testapp in the Garnet
      # CI/CQ  because it uses the real network. We run cobalt_testap_no_network
      # instead. This version is run in the stand-alone cobalt_client CI.
      disabled = true
    },
  ]
}

executable("bin") {
  output_name = "cobalt"

  sources = [
    "cobalt_main.cc",
  ]

  deps = [
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/cobalt/fidl:fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//third_party/cobalt/config:cobalt_config_proto",
    "//third_party/cobalt/encoder",
    "//third_party/grpc:grpc++",

    # TODO(rudominer) Change to protobuf_lite after we have a different way
    # of deserializing a message from a human-readable string.
    "//third_party/protobuf:protobuf_full",
  ]
}

executable("cobalt_system_metrics_bin") {
  output_name = "cobalt_system_metrics"

  sources = [
    "system_metrics_main.cc"
  ]

  deps = [
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/cobalt/fidl:fidl",
  ]
}

executable("cobalt_unittests") {
  testonly = true

  deps = [
    "//third_party/cobalt/encoder:unittests",
  ]
}

executable("cobalt_testapp") {
  sources = [
    "cobalt_testapp.cc",
  ]

  deps = [
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/cobalt/fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/svc/cpp",
  ]
}

copy("cobalt_testapp_no_network") {
  sources = [
    "cobalt_testapp_no_network",
  ]

  outputs = [
    "$root_out_dir/cobalt_testapp_no_network",
  ]
}

copy("cobalt_testapp_no_environment") {
  sources = [
    "cobalt_testapp_no_environment",
  ]

  outputs = [
    "$root_out_dir/cobalt_testapp_no_environment",
  ]
}
