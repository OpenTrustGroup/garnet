# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

static_library("lib") {
  sources = [
    "debug_agent.cc",
    "debug_agent.h",
    "debugged_process.cc",
    "debugged_process.h",
    "exception_handler.cc",
    "exception_handler.h",
    "launcher.cc",
    "launcher.h",
    "object_util.cc",
    "object_util.h",
    "process_info.cc",
    "process_info.h",
    "system_info.cc",
    "system_info.h",
  ]

  public_deps = [
    "//garnet/lib/debug_ipc:agent",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/launchpad",
  ]
}

executable("bin") {
  output_name = "debug_agent"
  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]
}

package("debug_agent") {
  system_image = true
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "debug_agent"
    },
  ]
}

executable("unittests") {
  testonly = true
  output_name = "debug_agent_tests"

  sources = [
    "process_info_unittests.cc",
    "system_info_unittests.cc",
  ]

  deps = [
    ":lib",

    # This target also runs these debug_ipc tests.
    "//garnet/lib/debug_ipc:tests",
    "//garnet/public/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

package("debug_agent_tests") {
  testonly = true
  system_image = true

  package_name = "debug_agent_tests"

  deps = [
    ":unittests",
  ]

  tests = [
    {
      name = "debug_agent_tests"
    },
  ]
}
