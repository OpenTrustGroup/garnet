# Copyright 2018 Open Trust Group
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
source_set("lib") {
  sources = [
    "ree_agent.cc",
    "ree_agent.h",
    "ree_message_impl.cc",
    "ree_message_impl.h",
    "ta_service.h",
    "tipc_agent.cc",
    "tipc_agent.h",
    "tipc_msg.h",
  ]

  public_deps = [
    "//garnet/public/lib/ree_agent/cpp",
    "//garnet/public/lib/ree_agent/fidl",
    "//garnet/public/lib/svc/cpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/async-loop-cpp",
  ]

  defines = [ "_ALL_SOURCE=1" ]
}

executable("bin") {
  output_name = "ree_agent"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
  ]
}

package("ree_agent") {
  deprecated_system_image = true

  deps = [
    ":bin",
  ]
  binaries = [
    {
      name = "ree_agent"
    },
  ]
}

executable("ree_agent_unittests") {
  visibility = [ ":*" ]
  testonly = true

  sources = [
    "run_all_unittests.cc",
    "ree_message-unittest.cc",
    "channel-unittest.cc",
    "object-unittest.cc",
    "port-unittest.cc",
    "tipc_agent_unittest.cc",
  ]

  deps = [
    ":lib",
    "//third_party/googletest:gtest",
  ]
}

package("ree_agent_tests") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":ree_agent_unittests",
  ]

  tests = [
    {
      name = "ree_agent_unittests"
    },
  ]
}
