# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/package.gni")

rustc_binary("bin") {
  name = "logger"
  with_unit_tests = true

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/crates/fuchsia-app",
    "//garnet/public/rust/crates/fuchsia-async",
    "//garnet/public/rust/crates/fuchsia-zircon",
    "//third_party/rust-crates/rustc_deps:byteorder",
    "//third_party/rust-crates/rustc_deps:failure",
    "//third_party/rust-crates/rustc_deps:futures",
    "//third_party/rust-crates/rustc_deps:libc",
    "//third_party/rust-crates/rustc_deps:parking_lot",
    "//third_party/rust-crates/rustc_deps:timebomb",
    "//zircon/public/fidl/logger:logger-rustc",
  ]
}

executable("logger_integration_cpp_tests") {
  testonly = true

  sources = [
    "test/logger_tests.cc",
    "test/run_all_tests.cc",
  ]
  deps = [
    "//third_party/gtest",
    "//zircon/public/lib/syslog",
  ]
}

package("logger") {
  deps = [
    ":bin",
  ]

  binary = "rust_crates/logger"

  meta = [
    {
      path = rebase_path("//garnet/bin/appmgr/legacy_flat_exported_dir")
      dest = "legacy_flat_exported_dir"
    },
  ]
}

package("logger_tests") {
  deprecated_system_image = true
  testonly = true

  deps = [
    ":bin",
    ":logger_integration_cpp_tests",
    "test/go/src/logger_integration_test:logger_integration_go_tests",
  ]

  tests = [
    {
      name = "logger_bin_test_rustc"
    },
    {
      name = "logger_integration_cpp_tests"
    },
    {
      name = "logger_integration_go_tests"
    },
  ]
}
