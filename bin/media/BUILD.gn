# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

group("services") {
  public_deps = [
    "//garnet/bin/media/codec_factory",
  ]
}

group("tools") {
  public_deps = [
    "//garnet/bin/media/signal_generator",
    "//garnet/bin/media/vol",
  ]
}

package("audio_core") {
  deps = [
    "//garnet/bin/media/audio_core",
    "//garnet/bin/media/audio_core/audiofx",
  ]

  binary = "audio_core"

  loadable_modules = [
    {
      # This audio processing library is hosted in the audio_core process.
      name = "audiofx.so"
    },
  ]

  meta = [
    {
      path = rebase_path("audio_core/meta/audio_core.cmx")
      dest = "audio_core.cmx"
    },
  ]
}

package("codec_factory") {
  deps = [
    "//garnet/bin/media/codec_factory:codec_factory",
  ]

  binary = "codec_factory"

  meta = [
    {
      path = rebase_path("codec_factory/meta/codec_factory.cmx")
      dest = "codec_factory.cmx"
    },
  ]
}

# This package is only really expected to be used by the codec_factory package.
package("codec_runner_sw_omx") {
  deps = [
    "//garnet/bin/media/codecs/sw/omx/codec_runner_sw_omx:codec_runner_sw_omx",

    # Codec loadable_module(s)
    "//garnet/bin/media/codecs/sw/omx/dec/aac:libcodec_sw_omx_dec_aac",
  ]

  binary = "codec_runner_sw_omx"

  loadable_modules = [
    {
      name = "libcodec_sw_omx_dec_aac.so"
    },
  ]
}

package("tests") {
  testonly = true
  deprecated_system_image = true

  package_name = "media_tests"

  deps = [
    "//garnet/bin/media/audio_core:tests",
    "//garnet/bin/media/audio_core/mixer:tests",
    "//garnet/public/lib/media/audio_dfx",
    "//garnet/public/lib/media/audio_dfx:tests",
    "//garnet/public/lib/media/timeline:tests",
    "//garnet/public/lib/media/transport:tests",
  ]

  loadable_modules = [
    {
      # This test-only library is hosted in the audio_dfx_tests process.
      name = "audio_dfx.so"
    },
  ]

  tests = [
    {
      name = "audio_core_tests"
    },

    {
      name = "audio_dfx_tests"
    },

    {
      name = "audio_mixer_tests"
    },

    {
      name = "media_lib_timeline_tests"
    },

    {
      name = "media_lib_transport_tests"
    },
  ]
}
