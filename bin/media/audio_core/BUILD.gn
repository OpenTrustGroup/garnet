# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")

action_foreach("generate_schemas") {
  visibility = [ ":*" ]
  script = "//garnet/bin/media/audio_core/schema/make_schema_header.py"
  outputs = [
    "$target_gen_dir/schema/{{source_name_part}}.inl",
  ]
  args = [
    "{{source}}",
    rebase_path("$target_gen_dir") + "/schema/{{source_name_part}}.inl",
  ]

  sources = [
    "schema/audio_device_settings_schema.jsx",
  ]
}

source_set("audio_mixer_lib") {
  sources = [
    "//garnet/public/lib/media/audio_dfx/audio_device_fx.h",
    "constants.h",
    "gain.cc",
    "gain.h",
    "mixer/fx_loader.cc",
    "mixer/fx_loader.h",
    "mixer/fx_processor.cc",
    "mixer/fx_processor.h",
    "mixer/linear_sampler.cc",
    "mixer/linear_sampler.h",
    "mixer/mixer.cc",
    "mixer/mixer.h",
    "mixer/mixer_utils.h",
    "mixer/no_op.cc",
    "mixer/no_op.h",
    "mixer/output_producer.cc",
    "mixer/output_producer.h",
    "mixer/point_sampler.cc",
    "mixer/point_sampler.h",
  ]

  public_deps = [
    "//garnet/public/fidl/fuchsia.media",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/media/timeline:no_converters",
    "//zircon/public/lib/fbl",
  ]
}

executable("audio_core") {
  sources = [
    "audio_core_impl.cc",
    "audio_core_impl.h",
    "audio_device.cc",
    "audio_device.h",
    "audio_device_manager.cc",
    "audio_device_manager.h",
    "audio_device_settings.cc",
    "audio_device_settings.h",
    "audio_driver.cc",
    "audio_driver.h",
    "audio_in_impl.cc",
    "audio_in_impl.h",
    "audio_input.cc",
    "audio_input.h",
    "audio_link.cc",
    "audio_link.h",
    "audio_link_packet_source.cc",
    "audio_link_packet_source.h",
    "audio_link_ring_buffer_source.cc",
    "audio_link_ring_buffer_source.h",
    "audio_object.cc",
    "audio_object.h",
    "audio_out_format_info.cc",
    "audio_out_format_info.h",
    "audio_out_impl.cc",
    "audio_out_impl.h",
    "audio_output.cc",
    "audio_output.h",
    "audio_packet_ref.cc",
    "audio_packet_ref.h",
    "audio_plug_detector.cc",
    "audio_plug_detector.h",
    "driver_output.cc",
    "driver_output.h",
    "driver_ring_buffer.cc",
    "driver_ring_buffer.h",
    "driver_utils.cc",
    "driver_utils.h",
    "fwd_decls.h",
    "main.cc",
    "pending_flush_token.cc",
    "pending_flush_token.h",
    "standard_output_base.cc",
    "standard_output_base.h",
    "throttle_output.cc",
    "throttle_output.h",
    "utils.cc",
    "utils.h",
  ]

  deps = [
    ":generate_schemas",
    "//garnet/bin/media/audio_core:audio_mixer_lib",
    "//garnet/bin/media/util",
    "//garnet/lib/media/wav_writer",
    "//garnet/public/fidl/fuchsia.media",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/media/audio",
    "//garnet/public/lib/media/timeline:no_converters",
    "//third_party/rapidjson",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/audio-proto-utils",
    "//zircon/public/lib/dispatcher-pool",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/zx",
  ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]
}

test("tests") {
  testonly = true
  output_name = "audio_core_tests"

  sources = [
    "test/audio_core_tests.cc",
    "test/audio_out_tests.cc",
  ]

  deps = [
    "//garnet/public/fidl/fuchsia.media",
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/gtest",
    "//third_party/googletest:gtest_main",
  ]
}
