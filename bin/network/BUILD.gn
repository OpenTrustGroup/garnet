# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

source_set("errors") {
  sources = [
    "net_error_list.h",
    "net_errors.cc",
    "net_errors.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
  ]
}

executable("bin") {
  # if you don't need HTTPS, comment out NETWORK_SERVICE_USE_HTTPS in |defines| below.
  defines = [ "NETWORK_SERVICE_USE_HTTPS" ]

  output_name = "network"

  sources = [
    "http_client.h",
    "main.cc",
    "net_adapters.cc",
    "net_adapters.h",
    "network_service_delegate.cc",
    "network_service_delegate.h",
    "network_service_impl.cc",
    "network_service_impl.h",
    "upload_element_reader.cc",
    "upload_element_reader.h",
    "url_loader_impl.cc",
    "url_loader_impl.h",
  ]

  deps = [
    ":errors",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/netstack/fidl:net_address",
    "//garnet/public/lib/network/fidl",
    "//garnet/public/lib/url",
    "//third_party/asio",
    "//third_party/boringssl:boringssl",
    "//zircon/public/lib/zx",
  ]
}

package("network") {
  deps = [
    ":bin",
  ]

  binary = "network"

  meta = [ {
        path = rebase_path("meta/sandbox")
        dest = "sandbox"
      } ]
}
