# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//garnet/public/lib/fidl/build/fidl/fidl.gni")

executable("bin") {
  output_name = "trace_manager"

  sources = [
    "app.cc",
    "app.h",
    "config.cc",
    "config.h",
    "main.cc",
    "trace_manager.cc",
    "trace_manager.h",
    "trace_provider_bundle.cc",
    "trace_provider_bundle.h",
    "trace_session.cc",
    "trace_session.h",
    "tracee.cc",
    "tracee.h",
  ]
  deps = [
    ":trace_link_fidl",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/tracing/fidl",
    "//third_party/rapidjson",
    "//zircon/public/lib/trace-provider",
  ]
}

fidl("trace_link_fidl") {
  name = "trace_link"
  sources = [
    "//zircon/system/ulib/trace-provider/fidl/trace_link.fidl",
  ]
}

package("trace_manager") {
  deps = [
    ":bin",
    ":trace_link_fidl",
  ]

  binary = "trace_manager"

  resources = [ {
        path = rebase_path("tracing.config")
        dest = "tracing.config"
      } ]
}
