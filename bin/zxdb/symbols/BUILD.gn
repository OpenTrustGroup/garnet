# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

static_library("symbols") {
  # The client library one directory up instantiates the "Impl" classes using
  # the non-public headers.
  friend = [
    ":*",
    "//garnet/bin/zxdb/client/*",
  ]

  public = [
    "array_type.h",
    "base_type.h",
    "code_block.h",
    "data_member.h",
    "dwarf_expr_eval.h",
    "file_line.h",
    "function.h",
    "lazy_symbol.h",
    "line_details.h",
    "loaded_module_symbols.h",
    "location.h",
    "modified_type.h",
    "module_symbol_status.h",
    "module_symbols.h",
    "namespace.h",
    "process_symbols.h",
    "symbol_data_provider.h",
    "system_symbols.h",
    "target_symbols.h",
    "struct_class.h",
    "symbol.h",
    "symbol_context.h",
    "symbol_factory.h",
    "symbol_utils.h",
    "type.h",
    "type_utils.h",
    "value.h",
    "variable.h",
    "variable_location.h",
  ]

  sources = [
    "array_type.cc",
    "base_type.cc",
    "code_block.cc",
    "data_member.cc",
    "dwarf_die_decoder.cc",
    "dwarf_die_decoder.h",
    "dwarf_expr_eval.cc",
    "dwarf_symbol_factory.cc",
    "dwarf_symbol_factory.h",
    "file_line.cc",
    "function.cc",
    "lazy_symbol.cc",
    "line_details.cc",
    "loaded_module_symbols.cc",
    "location.cc",
    "modified_type.cc",
    "module_symbol_index.cc",
    "module_symbol_index.h",
    "module_symbol_index_node.cc",
    "module_symbol_index_node.h",
    "module_symbols.cc",
    "module_symbols_impl.cc",
    "module_symbols_impl.h",
    "namespace.cc",
    "process_symbols.cc",
    "process_symbols_impl.cc",
    "process_symbols_impl.h",
    "system_symbols.cc",
    "target_symbols.cc",
    "target_symbols_impl.cc",
    "target_symbols_impl.h",
    "struct_class.cc",
    "symbol.cc",
    "symbol_utils.cc",
    "type.cc",
    "type_utils.cc",
    "value.cc",
    "variable.cc",
    "variable_location.cc",
  ]

  # This library can not use any part of the debugger other than the "common"
  # base helpers.
  public_deps = [
    "//garnet/bin/zxdb/common",
    "//garnet/lib/debug_ipc:client",
    "//garnet/lib/debug_ipc/helper",
    "//garnet/public/lib/fxl",
  ]

  deps = [
    "//garnet/third_party/llvm:LLVMDebugInfoDWARF",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "mock_module_symbols.cc",
    "mock_module_symbols.h",
    "mock_process_symbols.cc",
    "mock_process_symbols.h",
    "mock_symbol_data_provider.cc",
    "mock_symbol_data_provider.h",
    "type_test_support.cc",
    "type_test_support.h",
    "variable_test_support.cc",
    "variable_test_support.h",
  ]

  deps = [
    ":symbols",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "code_block_unittest.cc",
    "dwarf_expr_eval_unittest.cc",
    "dwarf_symbol_factory_unittest.cc",
    "dwarf_test_util.cc",
    "dwarf_test_util.h",
    "modified_type_unittest.cc",
    "module_symbol_index_unittest.cc",
    "module_symbol_index_node_unittest.cc",
    "module_symbols_impl_unittest.cc",
    "process_symbols_impl_unittest.cc",
    "symbol_utils_unittest.cc",
    "system_symbols_unittest.cc",
    "test_symbol_module.cc",
    "type_utils_unittest.cc",
    "variable_location_unittest.cc",
  ]

  deps = [
    ":symbols",
    ":test_support",
    "//third_party/googletest:gtest",
    "//garnet/third_party/llvm:LLVMDebugInfoDWARF",
  ]


  data_deps = [
    "test_data:copy_test_so(${default_toolchain}-shared)",
  ]
}
