# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")

driver_module("host") {
  output_name = "bthost"

  deps = [
    ":sources",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

source_set("sources") {
  sources = [
    "binding.c",
    "driver.cc",
    "gatt_host.cc",
    "gatt_host.h",
    "gatt_remote_service_device.cc",
    "gatt_remote_service_device.h",
    "host.cc",
    "host.h",
    "host_device.cc",
    "host_device.h",

    # FIDL servers
    "fidl/gatt_client_server.cc",
    "fidl/gatt_client_server.h",
    "fidl/gatt_remote_service_server.cc",
    "fidl/gatt_remote_service_server.h",
    "fidl/gatt_server_server.cc",
    "fidl/gatt_server_server.h",
    "fidl/helpers.cc",
    "fidl/helpers.h",
    "fidl/host_server.cc",
    "fidl/host_server.h",
    "fidl/low_energy_central_server.cc",
    "fidl/low_energy_central_server.h",
    "fidl/low_energy_peripheral_server.cc",
    "fidl/low_energy_peripheral_server.h",
    "fidl/profile_server.cc",
    "fidl/profile_server.h",
    "fidl/server_base.h",
  ]

  deps = [
    "//garnet/drivers/bluetooth/lib/gap",
    "//garnet/lib/bluetooth/c",
    "//garnet/lib/bluetooth/fidl:bluetooth_host",
    "//garnet/public/fidl/fuchsia.bluetooth",
    "//garnet/public/fidl/fuchsia.bluetooth.control",
    "//garnet/public/fidl/fuchsia.bluetooth.gatt",
    "//garnet/public/fidl/fuchsia.bluetooth.le",
    "//garnet/public/lib/fxl",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

# TODO(armansito): Enable debug assertions by default from here for all package
# dependencies when there is a way for it.
package("bthost") {
  deprecated_system_image = true

  deps = [
    ":host",
  ]

  drivers = [
    {
      name = "bthost.so"
    },
  ]
}
