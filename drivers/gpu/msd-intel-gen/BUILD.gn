# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")

shared_ldflags = "-Wl,--no-undefined"

INTEL_PCI_VID = "0x8086"

driver_module("msd_intel") {
  output_name = "libmsd_intel"

  deps = [
    ":msd_intel_entry",
    "src",
  ]

  ldflags = [ "$shared_ldflags" ]
}

driver_module("msd_intel_test") {
  testonly = true

  output_name = "libmsd_intel_test"

  deps = [
    ":msd_intel_test_entry",
    "src",
  ]

  ldflags = [ "$shared_ldflags" ]
}

source_set("msd_intel_entry") {
  pci_vendor_id = INTEL_PCI_VID
  public_configs = [ "$msd_intel_gen_build_root:msd_src_include_config" ]
  sources = [
    "src/driver_entry.cc",
  ]
  defines = [ "MAGMA_PCI_VENDOR_ID=" + pci_vendor_id ]
  deps = [
    "//garnet/lib/magma/src/magma_util",
    "//garnet/lib/magma/src/magma_util:command_buffer",
    "//garnet/lib/magma/src/magma_util/platform:trace",
    "//garnet/lib/magma/src/sys_driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/driver",
    "//zircon/public/lib/zx",
  ]
  libs = [ "zircon" ]
}

source_set("msd_intel_test_entry") {
  testonly = true

  pci_vendor_id = INTEL_PCI_VID
  public_configs = [ "$msd_intel_gen_build_root:msd_src_include_config" ]
  sources = [
    "src/driver_entry.cc",
  ]
  defines = [
    "MAGMA_PCI_VENDOR_ID=" + pci_vendor_id,
    "MAGMA_TEST_DRIVER=1",
  ]

  deps = [
    "//garnet/lib/magma/src/magma_util",
    "//garnet/lib/magma/src/magma_util:command_buffer",
    "//garnet/lib/magma/src/magma_util/platform:trace",
    "//garnet/lib/magma/src/sys_driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/driver",
    "//zircon/public/lib/zx",
  ]
  libs = [ "zircon" ]
  deps += [
    ":indriver_gtest",
    "//garnet/lib/magma/src/sys_driver:pci_driver_gtest",
  ]
}

group("tests") {
  testonly = true
  public_deps = [
    "tests/unit_tests:msd_intel_gen_nonhardware_tests",
  ]
}

group("indriver_gtest") {
  testonly = true

  deps = [
    "tests:gtest",
  ]
}

# This config allows inclusion of headers via relative paths
config("msd_tests_include_config") {
  include_dirs = [ "tests" ]
}

# This config allows inclusion of headers via relative paths
config("msd_src_include_config") {
  include_dirs = [ "src" ]
}
