# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/lib/magma/gnbuild/magma.gni")

cube_use_mozart = true

executable("vkcube_image_pipe_swapchain") {
  sources = [
    "main.cc",
  ]
  deps = [
    ":vkcube_common",
  ]
}

config("test_vk_cube") {
  include_dirs = [ "." ]
}

source_set("vkcube_common") {
  public_configs = [ ":test_vk_cube" ]

  defines = [
    "VK_USE_PLATFORM_MAGMA_KHR",
    "CUBE_USE_IMAGE_PIPE",
  ]

  sources = [
    "//garnet/lib/vulkan/third_party/vkcube/cube.cc",
    "//garnet/lib/vulkan/third_party/vkcube/cube.h",
    "test_vk_cube.h",
    "vkcube_view.cc",
    "vkcube_view.h",
  ]

  deps = [
    "$magma_build_root/src/magma_util",
    "$magma_build_root/src/magma_util/platform:trace",
    "//garnet/lib/magma/src/display_pipe",
    "//garnet/lib/magma/src/display_pipe/services",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/view_framework:view_provider",
    "//garnet/public/lib/ui/views/fidl:v1",
    "//garnet/public/lib/vulkan",
  ]

  if (cube_use_mozart) {
    defines += [ "CUBE_USE_MOZART" ]
    deps += [
      "//garnet/public/lib/ui/scenic",
      "//garnet/public/lib/ui/scenic:client",
    ]
  }
}
